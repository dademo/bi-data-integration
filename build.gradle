/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 */

plugins {
    id 'com.github.spotbugs' version "${spotbugsPluginVersion}" apply false
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    afterEvaluate {

        project.tasks.withType(Jar) {
            // We enable jar creation
            it.enabled = true
        }

        // Project in dependencies doesn't have BootJar enabled
        project.findAll {
            plugins.findPlugin('org.springframework.boot') &&
                [
                    ":modules:dependencies",
                    ":modules:supervision:backends",
                ].stream().map(it.getPath()::startsWith).map(Boolean::valueOf).anyMatch(Boolean.TRUE::equals)
        }.each {
            it.bootJar {
                enabled = false
            }
        }


        (project.tasks.withType(JavaCompile).findAll { it.enabled }).each {
            it.options.deprecation = true
            it.options.warnings = true
            it.options.compilerArgs << "-Xlint:unchecked"
        }

        (project.findAll { plugins.withType(JavaPlugin) }).each { _project ->

            if (!Boolean.parseBoolean(usesSonar)) {
                [
                    'checkstyle',
                    'pmd',
                    'com.github.spotbugs',
                ].each {
                    _project.plugins.apply(it)
                }
            }

            [
                'jacoco',
            ].each {
                _project.plugins.apply(it)
            }
        }

        (project.tasks.withType(Jar).findAll { it.enabled }).each {

            if (!Boolean.parseBoolean(usesSonar)) {
                if (new File(project.projectDir, 'config/checkstyle/checkstyle.xml').exists()) {
                    it.dependsOn checkstyleMain, spotbugsMain, pmdMain
                } else {
                    it.dependsOn spotbugsMain, pmdMain
                }
            }
        }

        (project.tasks.withType(Test).findAll { it.enabled }).each {

            it.useJUnitPlatform()
            it.failFast = false

            if (!Boolean.parseBoolean(usesSonar)) {
                if (new File(it.project.projectDir, 'config/checkstyle/checkstyle.xml').exists()) {
                    it.dependsOn spotbugsTest, pmdTest, checkstyleTest
                } else {
                    it.dependsOn spotbugsTest, pmdTest
                }
            }

            it.finalizedBy jacocoTestReport
        }


        if (project.plugins.withType(JacocoPlugin)) {

            project.tasks.findByName('jacocoTestReport')?.configure {

                it.dependsOn test

                it.reports {
                    html.required = true
                    xml.required = true
                    csv.required = false
                }
            }
        }
    }
}
